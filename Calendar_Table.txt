let
    StartDate = #date(2020, 2, 1),
    //StartDate = List.Min(Fact[Date]),
    EndDate = #date(2021,1,1),
    //EndDate = List.Max(Fact[Date]),
    Length = Duration.Days(EndDate - StartDate),
    Source = List.Dates(StartDate, Length, #duration(1, 0, 0, 0)),

    GetTable = Table.FromList(Source, Splitter.SplitByNothing(), null, null),
    RenameColumns = Table.RenameColumns(GetTable,{{"Column1", "Date"}}),
    ProperType = Table.TransformColumnTypes(RenameColumns,{{"Date", type date}}),

    InsertYearNumber = Table.AddColumn(ProperType, "Year", each Date.Year([Date]), Int64.Type),

    InsertQuarter = Table.AddColumn(InsertYearNumber, "Quarter", each Date.QuarterOfYear([Date]), Int64.Type),

    InsertMonthNumber = Table.AddColumn(InsertQuarter, "Month Number", each Date.Month([Date]), Int64.Type),
    InsertMonthName = Table.AddColumn(InsertMonthNumber, "Month Name", each Date.MonthName([Date]), type text),
    InsertShortMonthName = Table.AddColumn(InsertMonthName, "MMM", each Text.Start([Month Name], 3), type text),

    InsertWeekOfYear = Table.AddColumn(InsertShortMonthName, "Week of Year", each Date.WeekOfYear([Date]), Int64.Type),

    InsertDayOfYear = Table.AddColumn(InsertWeekOfYear, "Day of Year", each Date.DayOfYear([Date]), Int64.Type),
    InsertDayOfWeek = Table.AddColumn(InsertDayOfYear, "Day of Week", each Date.DayOfWeek([Date]) +1, Int64.Type),
    InsertDayOfMonth = Table.AddColumn(InsertDayOfWeek, "Day of Month", each Date.Day([Date]), Int64.Type),
    InsertDayName = Table.AddColumn(InsertDayOfMonth, "Day Name", each Date.DayOfWeekName([Date]), type text),    
    
    InsertShortDayName = Table.AddColumn(InsertDayName, "DDD", each Text.Start([Day Name], 3), type text),
    
    InsertShortMonthYearName = Table.AddColumn(InsertShortDayName, "MMM-YY", each [MMM] & "-" &Text.From([Year]-2000)),
    InsertShortMonthYearOrder = Table.AddColumn(InsertShortMonthYearName, "MMM-YY order", each Text.From([Year]-2000)&Text.PadStart(Text.From([Month Number]), 2, "0")),
    Calendar = Table.ReorderColumns(InsertShortMonthYearOrder,{"Date", "Year", "Quarter", "Month Number", "Month Name", "MMM", "Day of Year", "Day of Month", "Day of Week", "Day Name", "DDD", "MMM-YY"})
in
    Calendar
